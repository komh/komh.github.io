<HTML XPOS=20% YPOS=BOTTOM WIDTH=80% HEIGHT=100%>
<HEAD>
<META HTTP-EQUIV="CONTENT-TYPE" CONTENT="TEXT/HTML; CHARSET=UTF-8">
<!-- extra bytes: 03 00 14 14 00 08 00 11 50 00 64 00 --><TITLE>Sqrt routine</TITLE>
</HEAD>
<BODY>
<H1>Sqrt routine</H1><!-- entering slot 548 --><A AUTO VIEWPORT DEPENDENT GROUP=4100 HREF="107_L4_Menu.html">[Autolink] Menu</A><P><!-- extra bytes: 4C 06 04 10 04 00 -->
<P>

<P>
SQRT-Routine from<B> Bob Price</B> (see <A HREF="958_L2H_EMailaddressesP.html">EMail
Addresses</A>)
<BR>
Captured from a message in a public CompuServe Forum
<P>

<PRE> 
/* ------------------------------------------------------------------ */
/* REXX exec to calculate a square root using an optimized            */
/* version of the ever-popular Newton-Raphson method.                 */
/* Alter numeric digits for desired level of precision.               */
/*                                                                    */
/* Author: Bob Price                                                  */
/**********************************************************************/

  numeric digits 21

  do forever
    call CharOut, "Enter a number (RETURN to exit): "
    thisNumber = strip( lineIN() )

    select
      when thisNumber = "" then
        leave

      when datatype( thisNumber ) &lt;&gt; "NUM" then
        say "Invalid response!"

      when thisNumber &lt;= 0 then
        say "Invalid number!"

      otherwise
      do
        thisSqrt = SQRT( thisNumber )
        Say " The Square root of " || thisNumber || " is " || thisSqrt
        Say " Controlvalue: "
        Say " " || thisSqrt "*" thisSqrt "=" thisSqrt*thisSqrt
      end /* otherwise */
    end /* select */
  end /* do forever */

exit 0

/* ------------------------------------------------------------------ */
/* function: calculate the square root of a number                    */
/*                                                                    */
/* call:     Sqrt number                                              */
/*                                                                    */
/* where:    number - number to calculate the square root of          */
/*                                                                    */
/* returns:  the square root                                          */
/*                                                                    */
SQRT: PROCEDURE
  ARG n
        /* ans, the "answer", will be brought to the square root by   */
        /* successive approximation.                                  */
  ans = n / 2                   /* initial guess at an answer         */
  prevans = n                   /* "previous answer"                  */
  do until prevans = ans        /* i.e., no more improvement is found */
     prevans = ans              /* save previous answer               */
     ans = ( prevans + ( n / prevans ) ) / 2
  end
return ans

</PRE>

<P>


<P><HR>

<A HREF="526_L3_SQRTroutine.html">[Back: SQRT routine]</A> <BR>
<A HREF="528_L3_Mathfunctions.html">[Next: Math. functions]</A> 
</BODY>
</HTML>
