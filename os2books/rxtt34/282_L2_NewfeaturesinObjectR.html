<HTML XPOS=20% YPOS=BOTTOM WIDTH=80% HEIGHT=100%>
<HEAD>
<META HTTP-EQUIV="CONTENT-TYPE" CONTENT="TEXT/HTML; CHARSET=UTF-8">
<!-- extra bytes: 03 00 14 14 00 08 00 11 50 00 64 00 --><TITLE>New features in Object REXX that are useful in Classic REXX programs also</TITLE>
<STYLE>
PRE {
  FONT-FAMILY: Consolas, "Lucida Console", "Courier New", Courier, MONOSPACE;
}
</STYLE>
</HEAD>
<BODY>
<H1>New features in Object REXX that are useful in Classic REXX programs also</H1><!-- entering slot 290 --><A AUTO VIEWPORT DEPENDENT GROUP=4100 HREF="107_L4_Menu.html">[Autolink] Menu</A><P><!-- extra bytes: 4C 06 04 10 04 00 -->
<P>

<P>
This section contains a list of some new features in <A HREF="269_L1_HintsforObjectREXX.html">Object-Oriented
REXX</A> that are useful even for Classic REXX programs.
<P>
Please do not forget to check the version of the REXX interpreter you're
using with your REXX programs before you attempt to use on of these features!

<P>
<!--0xfb-->---------- * ----------
<BR>

<P>
<B>DO i OVER stem</B>
<P>
This is a very useful enhancement of DO loops. Now you can simply walk over
all elements of a stem without knowing the tails:
<PRE> 
                    /* drop all stem entries                         */
  drop myStem.

  do i = 1 to 40
    j = random( 400 )
    myStem.j = 1
  end /* do i = 1 to 40 */

  do i over myStem.
                    /* "i" contains the name of the next tail         */
                    /* (to get the value use "myStem.i")              */
    say 'The ' || i || ' was at least one time set'
    say 'The ' || myStem.i || ' was at least one time set'
  end /* do i over myStem */
</PRE>

<P>

<P>
<!--0xfb-->---------- * ----------
<BR>

<P>
<B>Returning a stem variable</B>
<P>
In <A HREF="269_L1_HintsforObjectREXX.html">Object-Oriented REXX</A> a routine
can return a stem variable. Example:
<PRE> 
/* */
  test. = test1()

  do i = 1 to test.0
    say test.i
  end

return

test1:
                    /* init a local stem ...                          */
  a.0 = 3
  a.1 = 11
  a.2 = 22
  a.3 = 33
                    /* ... and return it to the calling routine.      */
return a.
</PRE>

<P>

<P>
<!--0xfb-->---------- * ----------
<BR>

<P>
Calculations and other stem variables are now possible to get or set a stem
variable. Example:
<PRE> 
/* init a stem with sample values                                     */
  do i = 1 to 500;
    test.i = i * 2
  end /* do */
  test.0 = 500

/* access the stem in the Classic way                                 */

  say test.5        /* (1)                                            */

  j = 4+55          /* (2)                                            */
  say test.j

  i = test.4        /* (3)                                            */
  say test.i

  i = test.4        /* (4)                                            */
  j = test.i
  say test.j

/* use the new way                                                    */

  say test.[5]      /* (1)                                            */

  say test.[4+55]   /* (2)                                            */

  say test.[test.4] /* (3)                                            */

                    /* (4)                                            */
  say test.[test.[test.4]]
</PRE>

<P>
<!--0xfb-->---------- * ----------
<BR>

<P>
<B>PARSE [upper|lower|caseless]</B>
<P>
<A HREF="253_L2_ThePARSEinstruction.html">The PARSE instruction</A> now
supports lower and caseless parsing.
<P>
<!--0xfb-->---------- * ----------
<BR>

<P>
Call-by-Reference-Parameters are now possible for internal and external
REXX procedures (at least for stem variables):
<PRE> 
/* */

  say
  say 'Sample code to show the usage of USE ARG in Object REXX '
  say

  j.0 = 2
  j.1 = 111
  j.2 = 222

  say 'Values of the variables before calling the sub routine:'
  say
  say '  j.0 is ' || j.0
  do k = 1 to j.0
    say '   j.' || k || ' is ' || j.k
  end /* do */

  say
  say 'Now calling TestUseArg ...'
  say
  call TestUseArg j.

  say
  say 'Values of the variables after calling the sub routine:'
  say
  say '  j.0 is ' || j.0
  do k = 1 to j.0
    say '   j.' || k || ' is ' || j.k
  end /* do */

exit

TestUseArg: PROCEDURE
  use arg local_j.

                    /* local_j points to the global stem j.           */
  local_j.0 = 3
  local_j.1 = '111 - one'
  local_j.2 = '222 - two'
  local_j.3 = '333 - three'

return
</PRE>

<P>
<!--0xfb-->---------- * ----------
<BR>

<P>
CALL</A> now accepts variables that are evaluated before the CALL statement
is executed:
<PRE> 
/*                                                                    */

  myRoutine = 'MYTEST'
  call (myRoutine)
exit

MYTEST:
  say 'This is mytest!'
RETURN
</PRE>

<P>

<P>
Plese note that the contents of the variable must be in uppercase if calling
an internal routine. This is not clearly stated in the online help.
<P>
<!--0xfb-->---------- * ----------
<BR>

<P>
DATE</A> is not restricted to the current date any more. Now you can use
the results of this function for date calculations (e.g. How many days are
between day 1 and day 2?).
<P>
<!--0xfb-->---------- * ----------
<BR>

<P>
STREAM</A> now supports some more commands and options; for example<B> FLUSH</B>,
<B>REPLACE</B> (rewrite a file without doing a DEL first), and<B> NOBUFFER</B>.
It also supports line-related positioning for files with fixed and variable
length records. And there are now two different file pointers for every
file -- one for reading from it and one for writing to it.
<P>
<!--0xfb-->---------- * ----------
<BR>

<P>
<!-- entering slot 291 --> TIME</A> is not restricted to the current time
any more. This allows calculations with time stamps.
<P>
<!--0xfb-->---------- * ----------
<BR>

<P>
Use the environment<B> .local</B> for variables global to the current process.
Example:
<PRE> 
/* create a variable global to the current process                    */
  .local['BS.MYVAR'] = 'This is a global variable'

/* call an external REXX routine to show that it works                */
  call rexxtry "say .local['BS.MYVAR'] "

  call rexxtry ".local['BS.MYVAR'] = 'This variable is set by REXXTRY'"

  say .local['BS.MYVAR']

/* you can also use the following code to read the variable           */
/* But be aware of the search order for environment symbols in        */
/* Object REXX!                                                       */
  say .BS.MYVAR

</PRE>

<P>

<P>
<B>Note: </B> "To avoid conflicts with future REXX-defined entries, it is
recommented that entries you place in the program local environment or in
the global environment include a least one period in the entry name."<!-- lm: 0x2 1 -->
<UL>
<P>

<P>
<!--0xfb--><LI><LI><LI><LI><LI><LI><LI><LI><LI><LI><LI><LI><LI><LI><LI><LI><LI><LI><LI><LI><LI>
<BR>

<P>
Use the environment<B> .environment</B> for variables global to all REXX
programs. Example:
<PRE> 
/* create a variable global to all REXX programs                      */
  .environment['BS.MYVAR'] = 'This is a global variable'

/* start a REXX program in another process to show that it works      */
  "cmd /c rexxtry say .environment['BS.MYVAR'] "

  "cmd /c rexxtry .environment['BS.MYVAR'] = 'This variable is set by REXXTRY'"

  say .environment['BS.MYVAR']

/* you can also use the following code to read or change the variable */
  say value( 'BS.MYVAR',,'' )

  call value 'BS.MYVAR', 'Value set using the VALUE function', ''
</PRE>

<P>
Note that the environment<B> .environment</B> contains a lot of default
variables that you should<B> NOT</B> change. To get a list of all existing
variables you can use the following code:
<PRE> 
/* show all variables in the environment .environment                 */
  do i over .environment
    say 'Variable "' || i || '" is "' || .environment[i] || '"'
  end /* do i over .envrionment */
</PRE>

<P>
To check if a variable is already defined, you can use the following code:

<PRE> 
/* check if a variables is defined in the environment .environment    */
  globalVar = 'BS.MYVAR'
  if .environment[globalVar] = .NIL then
    say 'Environment symbol "' || globalVar || '" is not defined.'
  else
  do
    say 'Environment symbol "' || globalVar || '" is defined;'
    say 'the value is "' || .environment[globalVar] || '".'
  end /* else */
</PRE>

<P>

<P>
<B>Note: </B> "To avoid conflicts with future REXX-defined entries, it is
recommented that entries you place in the program local environment or in
the global environment include a least one period in the entry name."<!-- lm: 0x2 1 -->
<P>

<P>
<!--0xfb--><LI><LI><LI><LI><LI><LI><LI><LI><LI><LI><LI><LI><LI><LI><LI><LI><LI><LI><LI><LI><LI>
<BR>

<P>
Use the directives<B> ::REQUIRES</B> and<B> ::ROUTINE</B> to implement external
routines.
<P>
<!--0xfb-->---------- * ----------
<BR>

<P>
Use the keyword instruction<B> RAISE</B> to raise a condition in the calling
routine. This is very handy to avoid endless return code evaluations.
<P>
Example:
<PRE> 
/* ------------------------------------------------------------------ */
/* save this code in the file 'TEST1.CMD'                             */

  parse source . . thisFile
  say 'This is ' || thisFile || '.'

                    /* install an error handler for user condition 4  */
  call on user 4 name ErrorRaised

  say 'Now calling TEST2.CMD. TEST2.CMD will raise an user condition ...'

                    /* now call TEST2.CMD; test2.cmd will raise an    */
                    /* user condition                                 */
  call TEST2.CMD

  say 'Now ending the test program.'
exit

/* ------------------------------------------------------------------ */
/* simple handler for the user condition 4                            */
ErrorRaised:
  say
  say '*** start of user condition handler ***'
  say
  say 'Condition "' || condition( 'C') || '" raised in line ' || ,
      sigl || '.'
  say 'The condition description is "' || condition( 'D' ) ||  '".'
  say 'Additional information is "' || condition( 'A' ) || '".'
  say
  say '*** end of user condition handler ***'
  say
return
</PRE>

<P>

<PRE> 
/* ------------------------------------------------------------------ */
/* save this code to TEST2.CMD                                        */

  parse arg thisArgs

  parse source . . thisFile
  say '  This is "' || thisFile || '".'
  say '   Called with "' || thisArgs || '".'

                    /* raise a user condition to the caller           */
  raise user 4 ,
        Description 'This is a user error' ,
        Additional 'This is additional information'
exit
/* ------------------------------------------------------------------ */
</PRE>

<P>
<!--0xfb-->---------- * ----------
<BR>

<P>
And there are a lot of very useful new functions in the REXXUTIL.DLL. See
<A HREF="327_L2_NewREXXUTILfunctions.html">New REXXUTIL functions in Object
REXX</A> for an overview of the new functions. This section also contains
information on how to use the new REXXUTIL.DLL with Classic REXX.
<P>
Again, read the section MIGRATION</A> in the Online-Help of <A HREF="269_L1_HintsforObjectREXX.html">Object-Oriented
REXX</A> -- else you might miss some of the new features.
<P>
 

<P><HR>

<A HREF="281_L3_TheSignalhandlinginO.html">[Back: The Signal handling in Object REXX]</A> <BR>
<A HREF="283_L2_HintsforusingObjectR.html">[Next: Hints for using Object REXX]</A> 
</BODY>
</HTML>
