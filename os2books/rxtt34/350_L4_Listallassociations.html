<HTML XPOS=20% YPOS=BOTTOM WIDTH=80% HEIGHT=100%>
<HEAD>
<META HTTP-EQUIV="CONTENT-TYPE" CONTENT="TEXT/HTML; CHARSET=UTF-8">
<!-- extra bytes: 03 00 14 14 00 08 00 11 50 00 64 00 --><TITLE>List all associations</TITLE>
</HEAD>
<BODY>
<H1>List all associations</H1><!-- entering slot 361 --><A AUTO VIEWPORT DEPENDENT GROUP=4100 HREF="107_L4_Menu.html">[Autolink] Menu</A><P><!-- extra bytes: 4C 06 04 10 04 00 -->
<P>

<P>
File      OS2.INI
<BR>
Application  PMWP_ASSOC_TYPE
<BR>
Keys      {association types}
<BR>
       (e.g. "Plain Text")
<BR>
Value     "00"x (no association)
<BR>
       or a list of object
<BR>
       handles separated with
<BR>
       zero bytes ("00"x)
<BR>
Description  This application contains
<BR>
       all known associations by
<BR>
       type
<BR>
Created    <A HREF="1040_L0_.html">?</A>
<BR>
Deleted    <A HREF="1040_L0_.html">?</A>
<BR>

<P>

<P>
File      OS2.INI
<BR>
Application  PMWP_ASSOC_FILTER
<BR>
Keys      {association filters}
<BR>
       (e.g. "*.CMD")
<BR>
Value     "00"x (no association)
<BR>
       or a list of object
<BR>
       handles separated with
<BR>
       zero bytes ("00"x)
<BR>
Description  This application contains
<BR>
       all known associations by
<BR>
       filter
<BR>
Created    <A HREF="1040_L0_.html">?</A>
<BR>
Deleted    <A HREF="1040_L0_.html">?</A>
<BR>

<P>

<P>
You can use the keys of these applications to get the known associations
and the data of the associated objects. 
<P>
<B>Note: </B> This program needs the routines from the section <A HREF="357_L4_Generalroutinesforth.html">General
routines for the samples</A>!<!-- lm: 0x2 1 -->
<UL>
<P>
 
<PRE> 
/*                                                                    */
/* Sample program to display a list of the associations               */
/*                                                                    */
/* Usage: ShowAsso {&gt;logfile}                                         */
/*                                                                    */
/*                                                                    */
/* Note:  This program needs Henk Kelders excellent DLL <A HREF="663_L3_WPTOOLS.html">WPTOOLS.DLL</A>!  */
/*                                                                    */
/*        Tested under OS/2 WARP Connect. May not work under other    */
/*        OS/2 versions!                                              */
/*                                                                    */
/* History                                                            */
/*   14.01.1996 v1.00 /bs                                             */
/*     - initial release (for RXT&amp;T v2.00)                            */
/*                                                                    */
/* (c) 1996 <A HREF="002_L1_GeneralForeword.html">Bernd Schemmer</A>, Germany, EMail: Bernd.Schemmer@gmx.de     */
/*                                                                    */

                    /* turn on the NOVALUE condition                  */
  signal on NOVALUE

                    /*  load REXXUTIL functions                       */
  call rxFuncAdd "SysLoadFuncs", "REXXUTIL", "SysLoadFuncs"
  call SysLoadFuncs

                    /* get all filter associations                    */
  thisRC = SysIni( "USER", "PMWP_ASSOC_FILTER", "ALL:", "assoc_filter" )

                    /* get all type associations                      */
  thisRC = SysIni( "USER", "PMWP_ASSOC_TYPE", "ALL:", "assoc_type" )


  call lineOut , "Associations by filter"
  call LineOut , "======================"
  call LineOut , ""

                    /* display filter associations                    */
  do i = 1 to assoc_filter.0
    curFilter = assoc_filter.i
    curHandle = SysIni( "USER", "PMWP_ASSOC_FILTER", curFilter )

    select

      when curHandle = "ERROR:" then
        call lineOut , "  " || curFilter || ,
                       ": Error retrieving the value for this key!"

      when curHandle = "00"x then
        call lineOut , "  " || curFilter || ,
                       ": No association for this filter."

      otherwise
      do
        call CharOut , "  " || curFilter || ": "

        cur2Indent = length( curFilter ) + 4 +2
        curIndent = 0

                    /* show the data of the associated objects        */
        do until curHandle = ""

                    /* handle multiple associations                   */
          parse var curHandle curSubHandle "00"x curHandle
          call ShowObjectData "#" || d2x( curSubHandle ) ,,
                              cur2Indent, curIndent
          curIndent = cur2Indent -2
        end /* until curHandle = "" */

      end /* otherwise */

    end /* select */
    call LineOut , ""

  end /* do i = 1 to assoc_filter.0 */

  call lineOut , "Associations by type"
  call LineOut , "===================="
  call LineOut , ""

                    /* display filter associations                    */
  do i = 1 to assoc_type.0
    curType = assoc_type.i
    curHandle = SysIni( "USER", "PMWP_ASSOC_TYPE", curType )

    select

      when curHandle = "ERROR:" then
        call lineOut , "  " || curType || ,
                       ": Error retrieving the value for this key!"

      when curHandle = "00"x then
        call lineOut , "  " || curType || ,
                       ": No association for this type."

      otherwise
      do
        call CharOut , "  " || curType || ": "

        cur2Indent = length( curType ) + 4 +2
        curIndent = 0

                    /* show the data of the associated objects        */
        do until curHandle = ""
                    /* handle multiple associations                   */
          parse var curHandle curSubHandle "00"x curHandle
          call ShowObjectData "#" || d2x( curSubHandle ) ,,
                              cur2Indent, curIndent
          curIndent = cur2Indent -2
        end /* until curHandle = "" */

      end /* otherwise */

    end /* select */
    call LineOut , ""

  end /* do i = 1 to assoc_type.0 */

exit

/* ------------- insert the routines from the section ------------- */
/*                 <A HREF="357_L4_Generalroutinesforth.html">General routines for the samples</A>                 */
/* ---------------------------- here! ----------------------------- */

</PRE>

<P>
 

<P><HR>

<A HREF="349_L4_Listallobjects.html">[Back: List all objects]</A> <BR>
<A HREF="351_L4_Getthedesktopdirecto.html">[Next: Get the desktop directory]</A> 
</BODY>
</HTML>
