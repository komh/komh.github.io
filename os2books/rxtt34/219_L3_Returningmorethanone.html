<HTML XPOS=20% YPOS=BOTTOM WIDTH=80% HEIGHT=100%>
<HEAD>
<!-- extra bytes: 03 00 14 14 00 08 00 11 50 00 64 00 --><TITLE>Returning more than one value from a function</TITLE>
</HEAD>
<BODY>
<H1>Returning more than one value from a function</H1><!-- entering slot 216 --><A AUTO VIEWPORT DEPENDENT GROUP=4100 HREF="107_L4_Menu.html">[Autolink] Menu</A><P><!-- extra bytes: 4C 06 04 10 04 00 -->
<P>

<P>
To write functions returning more than one value you can use the following
technique.
<PRE> 
/* simple program to show a technique using a function which returns  */
/* more than one value                                                */
/*                                                                    */
/* This example uses a variable character for the separator in the    */
/* returned value.                                                    */
/*                                                                    */

  say "Values returned by the function MyFunc:"
  parse value myFunc() with separator 2 rc1 (separator) rc2 (separator) rc3

  if separator &lt;= "A" then
    say " The separator for the return codes is: ASCII(" || ,
        C2D( separator ) || ")"
  else
    say " The separator for the return codes is: " || ,
        separator

  say " rc1 = """ || rc1 || """"
  say " rc2 = """ || rc2 || """"
  say " rc3 = """ || rc3 || """"
exit 0

/* simple function - does nothing than returning three values         */
myFunc: PROCEDURE

  separator = "00"x     /* character to separate the values in the    */
                        /* returned string                            */
                        /* Note that this can be any value from "00"x */
                        /* to "FF"x not used in one of the return     */
                        /* codes. The calling procedure need not      */
                        /* know the value before calling this         */
                        /* function!                                  */


                        /* sample return codes                        */
  rc1 = 4
  rc2 = "TestRC"
  rc3 = 0

RETURN separator || rc1 || separator || rc2 || separator || rc3

</PRE>

<P>
 

<P><HR>

<A HREF="218_L3_Informationsavedduri.html">[Back: Information saved during subroutine & function execution]</A> <BR>
<A HREF="220_L3_CatchingCTRLC.html">[Next: Catching CTRL-C]</A> 
</BODY>
</HTML>
