<HTML XPOS=20% YPOS=BOTTOM WIDTH=80% HEIGHT=100%>
<HEAD>
<META HTTP-EQUIV="CONTENT-TYPE" CONTENT="TEXT/HTML; CHARSET=UTF-8">
<!-- extra bytes: 03 00 14 14 00 08 00 11 50 00 64 00 --><TITLE>Changing the Lockup page entry</TITLE>
<STYLE>
PRE {
  FONT-FAMILY: Consolas, "Lucida Console", "Courier New", Courier, MONOSPACE;
}
</STYLE>
</HEAD>
<BODY>
<H1>Changing the Lockup page entry</H1><!-- entering slot 364 --><A AUTO VIEWPORT DEPENDENT GROUP=4100 HREF="107_L4_Menu.html">[Autolink] Menu</A><P><!-- extra bytes: 4C 06 04 10 04 00 -->
<P>

<P>
You cannot change the Lockup page entry using SysSetObjectData</A> in WARP
3. The book <A HREF="632_L3_OS2211PowerTechnique.html">OS/2 2.11 Power Techniques
</A>contains some information about changing the lockup feature using the
INI file entry (Partial/Full Screen Lockup, Setting Automatic Lockup and
the Timeout Value, Auto Dim/No AutoDim, Reset the password, change the lockup
bitmap, change the lockup bitmap display options).
<P>
In WARP 4 you can change some of the Lockup page entries and activate the
Lockup using SysSetObjectData</A> (see <A HREF="396_L3_SetupstringsforWPDes.html">Setup
strings for WPDesktop objects</A> and also <A HREF="414_L4_ActivatingLockupnow.html">Activating
Lockup now</A>)
<P>
Another method to init the lockup page entries with default values (useful
for setting default values, but see also <A HREF="354_L4_SetorgettheWPSlockup.html">Set
or get the WPS lockup password</A> ):
<P>
Change the lockup page entries of an installed OS/2 system to meet your
needs. Then call the REXX program below to create a REXX cmd to copy the
lockup options to another machine:
<PRE> 
/*                                                                    */
/* sample program to create a CMD to copy the lockup options of this  */
/* machine to other machines.                                         */
/*                                                                    */
/* This program creates a program called "SETLKUP.CMD" in the         */
/* current directory. Execute this program on other machines to copy  */
/* the lockup options of this machine.                                */
/*                                                                    */
/* Note that this program only works on OS/2 WARP. To work under      */
/* OS/2 versions prior to WARP you must change the application name   */
/* PM_Lockup to 'Lock Up Workplace' (with the blanks but without the  */
/* quotes) and the key name LockupOptions to 'Lockup_options3' (also  */
/* without the quotes).                                               */
/*                                                                    */
/* To use the program under 2.11 and WARP you may use the REXXUTIL    */
/* function SysOS2Ver</A> to distinguish between the versions --         */
/* but please note that I did NOT test if it is possible to use the   */
/* same values for different OS/2 versions!                           */
/*                                                                    */
/* (c) 1996 <A HREF="002_L1_GeneralForeword.html">Bernd Schemmer</A>, Germany, EMail: Bernd.Schemmer@gmx.de     */
/*                                                                    */

                    /* load the necessary REXXUTIL function(s)        */
  call rxFuncAdd "SysIni", "REXXUTIL", "SysIni"

                    /* name of the REXX cmd to copy the options       */
  outFile = "SETLKUP.CMD"

                    /* carriage return and linefeed values            */
  crLF    = "0D0A"x

                    /* show a little message                          */
  call LineOut , "Creating the file"
  call LineOut , "  " || outFile
  call CharOut , "to copy the lockup options of this machine to other",
                 "machines  ... "

                   /* delete an existing target file                  */
  "@del " outFile "2&gt;NUl 1&gt;NUL"

                    /* get the current lockup options                 */
  curLockupOptions = SysIni( "USER", "PM_Lockup", "LockupOptions" )

                    /* create the REXX cmd to copy this options to    */
                    /* another machine                                */
  call LineOut outFile ,,
    "/* REXX cmd to set the lockup options */"             || crLF || ,
    "/* created on " || date() || " at " || time() || " */"|| crLF || ,
    ""                                                     || crLF || ,
    "  call rxFuncAdd 'SysIni', 'REXXUTIL', 'SysIni'"      || crLF || ,
    ""                                                     || crLF || ,
    "  call SysIni 'USER', 'PM_Lockup', 'LockupOptions',," || crLF || ,
    "            '" || c2x( curLockupOptions ) || "'x"     || crLF || ,
    ""                                                     || crLF || ,
    "  '@setboot /IBD:C:'"                                 || crLF || ,
    ""

  call lineOut , " done."
</PRE>

<P>
Note that the reboot<B> is</B> necessary to make the changes active! 

<P><HR>

<A HREF="352_L4_Getalistofallprinter.html">[Back: Get a list of all printers]</A> <BR>
<A HREF="354_L4_SetorgettheWPSlockup.html">[Next: Set or get the WPS lockup password]</A> 
</BODY>
</HTML>
