<HTML XPOS=20% YPOS=BOTTOM WIDTH=80% HEIGHT=100%>
<HEAD>
<!-- extra bytes: 03 00 14 14 00 08 00 11 50 00 64 00 --><TITLE>Unpack packed data</TITLE>
</HEAD>
<BODY>
<H1>Unpack packed data</H1><!-- entering slot 457 --><A AUTO VIEWPORT DEPENDENT GROUP=4100 HREF="107_L4_Menu.html">[Autolink] Menu</A><P><!-- extra bytes: 4C 06 04 10 04 00 -->
<P>

<P>

<P>
Unpack routine from<B> Steve Pitts</B> (see <A HREF="958_L2H_EMailaddressesP.html">EMail
Addresses</A>)
<BR>
Captured from a message in a public CompuServe Forum
<P>

<PRE> 
/* ------------------------------------------------------------------ */
/* function: unpack routine for packed data (from mainframes)         */
/*                                                                    */
/* call:     unpack packed_str {,num_dec}                             */
/*                                                                    */
/* where:    packed_str = the packed data                             */
/*           num_dec = number of decimals (def.: 0)                   */
/*                                                                    */
/*                                                                    */
/* returns:  the unpacked number or "" in case of an error            */
/*                                                                    */
Unpack: PROCEDURE
  parse arg packed_str, num_dec

  if num_dec = "" then
    num_dec=0

                        /* Convert packed data to hex and split into  */
                        /* number and sign portions                   */
  hex_str=c2x( packed_str )
  dec_str=left( hex_str, length( hex_str )-1 )
  packed_sign=right( hex_str, 1 )

                        /* Check that sign and numeric portions have  */
                        /* valid values                               */
  if verify( packed_sign, "ABCDEF" ) &gt; 0 then
    return ""

  if verify( dec_str, "0123456789" ) &gt; 0 then
    return ""

                        /* Are there enough digits for the decimal    */
                        /* point??                                    */
  if num_dec &gt; length( dec_str ) then
    return ""

                        /* If sign portion indicates a negative       */
                        /* number then oblige                         */
  if pos( packed_sign, "BD" ) &gt; 0 then
    dec_str=0-dec_str

                        /* If there is a decimal point then add it at */
                        /* the appropriate place                      */
  if num_dec &gt; 0 then
    dec_str=insert( ".", dec_str, length( dec_str )-num_dec )

RETURN dec_str
</PRE>

<P>
 

<P><HR>

<A HREF="441_L2_Dateconverting.html">[Back: Date converting]</A> <BR>
<A HREF="443_L3_EBCDICtoASCII&ASCIIt.html">[Next: EBCDIC to ASCII & ASCII to EBCDIC]</A> 
</BODY>
</HTML>
