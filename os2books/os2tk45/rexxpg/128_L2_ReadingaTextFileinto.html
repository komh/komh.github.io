<HTML>
<HEAD>
<META HTTP-EQUIV="CONTENT-TYPE" CONTENT="TEXT/HTML; CHARSET=UTF-8">
<TITLE>Reading a Text File into an Array</TITLE>
<STYLE>
PRE {
  FONT-FAMILY: Consolas, "Lucida Console", "Courier New", Courier, MONOSPACE;
}
</STYLE>
</HEAD>
<BODY>
<H1>Reading a Text File into an Array</H1><!-- entering slot 135 -->
<P>
REXX provides a Stream method, named MAKEARRAY, that reads the contents
of a stream into an array object. MAKEARRAY is very handy when you need
to read an entire file into memory for processing. You can read the entire
file with a single REXX clause--no looping is necessary.
<P>
The following example (CVIEW.CMD) uses the MAKEARRAY method to read the
entire CONFIG.SYS file into an array object. CVIEW displays selected lines
from CONFIG.SYS. A search argument can be specified when starting CVIEW:

<PRE>cview libpath
</PRE>

<P>
CVIEW will prompt for a search argument if you don't specify one.
<P>
If CVIEW finds the string, it displays the line on which the string is found.
CVIEW continues to prompt for new search strings until you enter "Q" in
response to the prompt.
<PRE>/* CVIEW -- display lines from CONFIG.SYS                            */
parse upper arg search_string      /* Get any command line argument  */
file=.stream~new('c:\config.sys')  /* Create stream object */
lines=file~makearray(line)         /* Read file into an array object */
                                   /* LINES points to the array obj. */
do forever
   if search_string='' then do     /* Prompt for user input          */
      say 'Enter a search string or Q to quit:'
      parse upper pull search_string
      if search_string='Q' then exit
   end  /* Do */
   do i over lines                  /* Scan the array                */
      if pos(search_string,translate(i))&gt;0 then do
         say i                      /* Display any line that matches */
         say '='~copies(20)
      end  /* Do */
   end /* do */
   search_string=''                 /* Reset for next search         */
end /* do */
</PRE>

<P>


<P><HR>

<A HREF="127_L2_ReadingaTextFile.html">[Back: Reading a Text File]</A> <BR>
<A HREF="129_L2_ReadingSpecificLines.html">[Next: Reading Specific Lines of a Text File]</A> 
</BODY>
</HTML>
